<single_input>  ::= SingleNewline NEWLINE
                  | SimpleLine line:<simple_stmt>
                  | ComplexLine line:<compound_stmt> NEWLINE
<file_input>    ::= FileInput lines:<file_lines>* ENDMARKER
<file_lines>    ::= FileNewline NEWLINE
                  | FileStmt stmt:<stmt>

<func_def>      ::= FuncDef 'def' name:NAME params:<parameters> '->' func_type:CLASS ':' body:<suite>

<parameters>    ::= Parameters '(' &params{param,param_list}:<params_list>? ')'
<params_list>   ::= SingleParam param:<parameter>
                  | ParamList param:<parameter> ',' param_list:<params_list>
<parameter>     ::= Parameter external:NAME? internal:NAME ':' param_type:CLASS

<suite>         ::= <simple_stmt>
                  | ComplexSuite NEWLINE INDENT stmt:<stmt> stmts:<stmt>* DEDENT

<stmt>          ::= <simple_stmt>
                  | <compound_stmt>

<simple_stmt>   ::= SimpleStmt op_expr:<op_expr> NEWLINE
<expr_stmt>     ::= PassStmt 'pass'
                  | ReturnStmt 'return' exprs:<op_expr_list>?
                  | ExprListStmt &exprs{op_expr,op_expr_list}:<op_expr_list>
<op_expr_list>  ::= SimpleOpExprList op_expr:<op_expr>
                  | OpExprList op_expr:<op_expr> ',' op_expr_list:<op_expr_list>

<op_expr>       ::= OpExpr left_ops:OPERATOR* expr:<expr> sub_op_exprs:<sub_op_expr>* right_ops:OPERATOR*
<sub_op_expr>   ::= SubOpExpr first_op:OPERATOR ops:OPERATOR* expr:<expr>

<expr_list>     ::= SimpleExprList expr:<expr>
                  | ExprList expr:<expr> ',' expr_list:<expr_list>
<expr>          ::= Expr atom:<atom> trailers:<trailer>*
<atom>          ::= ParenExpr '(' &exprs{op_expr,op_expr_list}:<op_expr_list>? ')'
                  | Name NAME
                  | Number NUMBER
                  | Ellipsis '...'
                  | Zilch 'Zilch'
                  | True 'True'
                  | False 'False'
<trailer>       ::= Args '(' &args{arg,arg_list}:<arg_list>? ')'
                  | Field '.' NAME

<compound_stmt> ::= <func_def>
                  | <class_def>
                  | <interface_def>
                  | <data_def>

<class_def>     ::= ClassDef 'class' @<common_def>
<interface_def> ::= InterfaceDef 'interface' @<common_def>
<data_def>      ::= DataDef 'data' @<common_def>

<common_def>    ::= CommonDef name:CLASS args:<arguments>? ':' body:<suite>

<arguments>     ::= Args '(' args:<arg_list>? ')'
<arg_list>      ::= SingleArg arg:<argument>
                  | ArgList arg:<argument> ',' arg_list:<arg_list>
<argument>      ::= <expr>
