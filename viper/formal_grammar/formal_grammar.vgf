<single_input>      ::= SingleNewline NEWLINE
                      | SingleLine line:<term>
<term>              ::= <stmt>
                      | <expr>

<file_input>        ::= FileInput lines:<file_line>* ENDMARKER
<file_line>         ::= FileNewline NEWLINE
                      | FileStmt stmt:<stmt>

# STATEMENTS

<stmt>              ::= SimpleStmt stmt:<plain_stmt> NEWLINE
                      | <if_stmt>
                      | <definition>

<plain_stmt>        ::= <return_stmt>
                      | <assign_stmt>
                      | <empty_stmt>
<return_stmt>       ::= ReturnStmt 'return' tests:<test_expr_list>?
<assign_stmt>       ::= AssignStmt lhs:<lhs_expr> '=' expr:<expr>
<empty_stmt>        ::= EmptyStmt 'pass'

<if_stmt>           ::= IfStmt 'if' cond:<test_expr> ':' then_body:<stmt_block> elif_stmts:<elif_stmt>* else_stmt:<else_stmt>?
<elif_stmt>         ::= ElifStmt 'elif' cond:<test_expr> ':' elif_body:<stmt_block>
<else_stmt>         ::= ElseStmt 'else' ':' else_body:<stmt_block>

<definition>        ::= <func_def>
                      | <class_def>
                      | <interface_def>
                      | <data_def>

<func_def>          ::= FuncDef 'def' name:NAME '(' params:<parameter>&*{,} ')' '->' func_type:CLASS ':' body:<stmt_block>
<parameter>         ::= Parameter external:NAME? internal:NAME ':' param_type:CLASS

<class_def>         ::= ClassDef 'class' name:CLASS args:<class_arguments>? ':' body:<class_stmt_block>
<class_arguments>   ::= ClassArguments '(' parents:CLASS&*{,} ')'
<class_stmt_block>  ::= SimpleEmptyClassStmt 'pass' NEWLINE
                      | CompoundEmptyClassStmt NEWLINE INDENT 'pass' NEWLINE DEDENT
                      | CompoundClassStmtBlock NEWLINE INDENT stmts:<class_stmt>+ DEDENT
                      | <class_stmt>
<class_stmt>        ::= Field modifier:<modifier> name:NAME ':' var_type:CLASS NEWLINE
                      | Method modifier:<modifier> func:<func_def>
<modifier>          ::= StaticModifier 'static' access:<access>?
                      | NonstaticModifier access:<access>?
<access>            ::= PublicAccess 'public'
                      | PrivateAccess 'private'
                      | ProtectedAccess 'protected'
                      | ModuleAccess 'module'

<interface_def>     ::= InterfaceDef 'interface' name:CLASS args:<arguments>? ':' body:<stmt_block>

<data_def>          ::= DataDef 'data' name:CLASS args:<arguments>? ':' body:<stmt_block>

<arguments>         ::= Arguments '(' args:<atom_expr>&*{,} ')'

<stmt_block>        ::= SimpleStmtBlock stmt:<stmt>
                      | CompoundStmtBlock NEWLINE INDENT stmts:<stmt>+ DEDENT

# LEFT-HAND SIDE EXPRESSIONS

<lhs_expr>          ::= <pattern>

# EXPRESSIONS

<expr>              ::= <if_expr>
                      | <test_expr_list>

<if_expr>           ::= IfExpr 'if' cond:<test_expr> ':' then_body:<expr_block> elif_exprs:<elif_expr>* else_expr:<else_expr>?
<elif_expr>         ::= ElifExpr 'elif' cond:<test_expr> ':' elif_body:<expr_block>
<else_expr>         ::= ElseExpr 'else' ':' else_body:<expr_block>

<test_expr_list>    ::= TestExprList tests:<test_expr>&+{,}

<test_expr>         ::= TestExpr test:<or_test_expr>
<or_test_expr>      ::= OrTestExpr tests:<and_test_expr>&+{or}
<and_test_expr>     ::= AndTestExpr tests:<not_test_expr>&+{and}
<not_test_expr>     ::= NegatedTestExpr 'not' op_expr:<op_expr>
                      | NotNegatedTestExpr op_expr:<op_expr>

<op_expr>           ::= OpExpr left_op:OPERATOR? atom:<atom_expr> sub_op_exprs:<sub_op_expr>* right_op:OPERATOR?
<sub_op_expr>       ::= SubOpExpr op:OPERATOR atom:<atom_expr>

<atom_expr>         ::= AtomExpr atom:<atom> trailers:<trailer>*
<atom>              ::= ParenAtom '(' tests:<test_expr_list>? ')'
                      | NameAtom name:NAME
                      | ClassAtom name:CLASS
                      | IntAtom num:INT
                      | FloatAtom num:FLOAT
                      | EllipsisAtom '...'
                      | TrueAtom 'true'
                      | FalseAtom 'false'
<trailer>           ::= Call '(' args:<test_expr>&*{,} ')'
                      | FieldAccess '.' field:NAME

<expr_block>        ::= SimpleExprBlock expr:<expr> NEWLINE
                      | IndentedExprBlock NEWLINE INDENT expr:<expr> NEWLINE DEDENT

# PATTERNS

<pattern>           ::= <typed_pattern>
<typed_pattern>     ::= TypedVariablePattern id:<var_id> ':' pat_type:CLASS
                      | TypedAnonymousPattern '_' ':' pat_type:CLASS
                      | TypedFieldPattern root:<expr> '.' field:<var_id> ':' pat_type:CLASS
                      | <simple_pattern>
<simple_pattern>    ::= SimpleVariablePattern id:<var_id>
                      | SimpleAnonymousPattern '_'
                      | SimpleFieldPattern root:<expr> '.' field:<var_id>
                      | SimpleParenPattern '(' patterns:<pattern>&+{,} ')'

# PATHS

<var_id>            ::= VarId id:NAME
<id>                ::= ClassId id:CLASS
                      | <var_id>
<path>              ::= Path id:<id> parts:<path_part>*
<path_part>         ::= PathPart '.' part:<id>
