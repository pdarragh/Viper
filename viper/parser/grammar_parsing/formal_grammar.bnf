<single_input>  ::= SingleNewline NEWLINE
                  | SimpleLine line:<simple_stmt>
                  | ComplexLine line:<compound_stmt> NEWLINE
<file_input>    ::= FileInput lines:<file_lines>* ENDMARKER
<file_lines>    ::= FileNewline NEWLINE
                  | FileStmt stmt:<stmt>

<func_def>      ::= FuncDef 'def' name:NAME @<param_list> '->' func_type:CLASS ':' body:<suite>

<parameters>    ::= Parameters '(' params:<parameter>&{,}? ')'
<parameter>     ::= Parameter external:NAME? internal:NAME ':' param_type:CLASS

<suite>         ::= SimpleSuite stmt:<simple_stmt>
                  | ComplexSuite NEWLINE INDENT stmt:<stmt> stmts:<stmt>* DEDENT

<stmt>          ::= <simple_stmt>
                  | <compound_stmt>

<simple_stmt>   ::= SimpleStmt expr_stmt:<expr_stmt> NEWLINE
<expr_stmt>     ::= PassStmt 'pass'
                  | ReturnStmt 'return' exprs:<op_expr_list>?
                  | ExprListStmt @<op_expr_list>
<op_expr_list>  ::= OpExprList op_exprs:<op_expr>&{,}
<op_expr>       ::= OpExpr left_ops:OPERATOR* expr:<expr> sub_op_exprs:<sub_op_expr>* right_ops:OPERATOR*
<sub_op_expr>   ::= SubOpExpr first_op:OPERATOR ops:OPERATOR* expr:<expr>

<expr_list>     ::= ExprList exprs:<expr>&{,}
<expr>          ::= Expr atom:<atom> trailers:<trailer>*
<atom>          ::= ParenExpr '(' expr_list:<op_expr_list>? ')'
                  | Name name:NAME
                  | Number num:NUMBER
                  | Ellipsis '...'
<trailer>       ::= Args '(' @<arg_list> ')'
                  | Field '.' field:NAME

<compound_stmt> ::= <func_def>
                  | <class_def>
                  | <interface_def>
                  | <data_def>

<class_def>     ::= ClassDef 'class' @<common_def>
<interface_def> ::= InterfaceDef 'interface' @<common_def>
<data_def>      ::= DataDef 'data' @<common_def>

<common_def>    ::= CommonDef name:CLASS args:<arguments>? ':' body:<suite>

<arguments>     ::= Arguments '(' @<arg_list> ')'
<arg_list>      ::= ArgList args:<argument>&{,}?
<argument>      ::= <expr>
